plugins {
    id 'com.android.library'
    id 'pl.allegro.tech.build.axion-release' version '1.21.1'
    id 'maven-publish'
    id 'jacoco'
}

version = scmVersion.version
group = 'com.github.unhappychoice.colorhash'

scmVersion {
    tag {
        prefix = ''
    }
}

android {
    namespace 'com.github.unhappychoice.colorhash'
    compileSdk 35

    defaultConfig {
        minSdk 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlin {
        compilerOptions {
            jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.robolectric:robolectric:4.16.1"
}


tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacoco(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.required = true
        html.required = true
        classDirectories.setFrom(
                files(
                        fileTree(
                                dir: './build/intermediates/javac/debug/compileDebugJavaWithJavac/classes',
                                excludes: ['**/R.class', '**/R$*.class', '**/BuildConfig.class', '**/Manifest*.*', '**/*$inlined$*.*']
                        ) + fileTree(
                                dir: './build/tmp/kotlin-classes/debug',
                                excludes: ['**/R.class', '**/R$*.class', '**/BuildConfig.class', '**/Manifest*.*', '**/*$inlined$*.*']
                        )
                )
        )
    }
    sourceDirectories.from files('src/main/kotlin')
    executionData.from files('build/jacoco/testDebugUnitTest.exec')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.unhappychoice'
                artifactId = 'color-hash'
                version = project.version
            }
        }
    }
}
